'==============================================
'= Акцент 7.0 
'= Создан : 04/03/2021 12:07:05
'= Автор  : admin
'= Код    : 2000 ЕСВ базовый
'= Метод  : ЕСВ Базовый (ФОТ)
'==============================================
'#include "definition.avb"

Sub Method_OnCalc(Tr)
	Dim AgSum, SumPMin

	With Tr
		If Not .Agent.Prop("Инвалид") And Not .Agent.Prop("Трудовой договор", .Date3)  Then

			AgSum = .DependsW.Sum

			If AgSum <> 0 Then
				If Not (.agent.Prop("Совместитель") And Not .agent.Prop("Состоит в трудовых отношениях")) Then 
					If AgSum > .Sum3 Then AgSum = .Sum3

					If Tr.WPeriod.Year < 2016 Then
						SumPMin = .Sum3 / 17
					ElseIf Tr.WPeriod.Year = 2016 Then
						SumPMin = .Sum3 / 25
					Else
						SumPMin = .Sum3 / 15
					End If

					If AgSum < SumPMin Then 
						If Not IsAgIsIll(.Agent, .WPeriod.ID) And CheckUnemployed(Tr) Then  
							AgSum = SumPMin
						End If
					End If
				End If

				.Tariff = AgSum
				.Sum = Round2(.Tariff * .Sum1 / 100, 2)
			Else
				.Tariff = 0
				.Sum = 0
			End If

		End If
		
	End With
End Sub
'---
'
'---
Sub Method_OnApply(Tr)
	Dim ParentCode 

	With Tr
		If .agent.Type <> 3 Then .agid = 0
		If .AgID = 0 Then Exit Sub

		Set ParentCode = Tr.Code
		.Sum1 = iif(Tr.WPeriod.Year < 2016, 33.2, ParentCode.Percent)		' % начислений
		.Sum2 = iif(Tr.WPeriod.Year < 2016, 23426, ParentCode.threshold) 	' предельная сумма
		.Sum3 = Workarea.UTable( USRTBL_LGT ).GetValue( 1, USRTBL_MAXCY, 2 )	' макс. льготный доход
		.Date3 = Tr.WPeriod.FirstDay

	End With
End Sub
'---
'
'---
Function CheckUnemployed(Tr)
	Dim DatIN,DatOUT,FrstDays,LastDay

	With Tr.Agent
		DatIN  = .DateIn
		DatOUT = .DateOut
	End With

	With Tr.WPeriod
		FrstDays = .FirstDay
		LastDay  = .LastDay
	End With

	If DatIN >= FrstDays And DatIN <= LastDay Then
		CheckUnemployed = False 
	ElseIf DatOUT >= FrstDays And DatOUT <= LastDay Then
		CheckUnemployed = False
	Else
		CheckUnemployed = True
	End If

End Function
'---
'
'---