'-----------
'	Идеал 063
'	модуль обновления 
'-----------

'#include "_ideal_update_func.avb"
'#include "definition.avb"

Option Explicit

Class UpdateLog
	Public LogFileName
	Public fail
	Private LogFile
	Private IsLogfileOpened

	Sub Class_Initialize
		LogFileName = "c:\accent.log"
		IsLogfileOpened = False
		fail = False
	End Sub

	Sub Class_Terminate
		If IsLogfileOpened Then LogFile.Close
	End Sub

	Sub Writelog(TextLog)
		If Not IsLogfileOpened Then OpenFile LogFileName
		If IsLogfileOpened Then LogFile.WriteLine TextLog
	End Sub

	Sub OpenFile(FileName)
		Dim FSO

		If FileName = "" Then Exit Sub
		Set FSO = CreateObject("Scripting.FileSystemObject")
		Set LogFile = FSO.CreateTextFile(FileName, True)
		
		IsLogfileOpened = Not (LogFile Is Nothing)

	End Sub

End Class

'-------------------
'
'-------------------
Sub Main(Updater)
	Dim RD, Path

	Set RD = CreateLibObject("Redirect")

	If App.AppType <> "DAO" Then 
		RunScript updater
	End If

	If Not updater.fail Then SetNewTax updater
	If Not updater.fail Then AddDepends updater
	If Not updater.fail Then updatetables updater
	If Not updater.fail Then updatemethod WorkArea.GetSysCodeID("&ОПП"), "Общие подпрограммы.mtd", updater
	If Not updater.fail Then updatemethod WorkArea.GetCodeID("2220п"), "2220п Алименты.mtd", updater

'	If Not updater.fail Then update_alim updater

	If Not updater.fail Then 
		If MsgBox("Показать что нового в программе ?", vbInformation + vbOKCancel, "Акцент-заработная плата Идеал") = vbOK Then
			ShowWhatsNew "WhatNewIdeal.chm", "063.htm"
		End If
	Else
		MsgBox "В процессе установки обновления произошли ошибки." & vbNewLine & _
					"Смотрите в log-файле более детальную информацию.", vbCritical, "Акцент. Установка обновлений"
	End If

End Sub
'----
'
'----
Sub updatemethod(CodeID, CodeFile, updater)
	Dim MtdFile

	MtdFile = CreateLibObject("Redirect").GetFullPath(CodeFile, True)

	If MtdFile = "" Then
		updater.writelog "Невозможно обновить метод. Файл '" & CodeFile & "'не найден."
		updater.fail = True
	Else

		If CodeID = 0 Then 
			updater.writelog "Метод '" & CodeFile & "' не найден"
			updater.fail = True
		Else
			Workarea.Code(CodeID).setScriptFromFile MtdFile
			updater.writelog "Метод '" & CodeFile & "' обновлен."
		End If
	End If
End Sub
'----
'
'----
Sub RunScript(updater)
	Dim RD

	Set RD = CreateLibObject("Redirect")
	RunSQLFile2 RD.GetFullPath("063.sql"), updater

	If updater.fail Then
		MsgBox "Не удалось выполнить скрипт на сервере." & vbCrLf & _
					"Файл : " & RD.GetFullPath("063.sql"), vbExclamation, "Внимание !"
		updater.fail = False
	End If


End Sub
'----
'
'----
Sub SetNewTax(updater)
	Dim UTableID

	If MsgBox("Обновлять ставки c января 2012 года ?", vbQuestion + vbOKCancel, "Акцент " & App.Version) = vbOK Then
		UTableID = Workarea.GetSysUTableID("ПДТ")

		If UTableID = 0 Then
			updater.writelog "Не найдена таблица с системным кодом ПДТ (Подоходный налог)"
			updater.fail = True
			Exit Sub
		End If
	
		UpdateUTFieldValue UTableID, "Максимальный доход для НДФЛ", 10730, updater
		UpdateUTFieldValue UTableID, "Максимальный доход", 18241, updater
		UpdateUTFieldValue UTableID, "Прожиточный минимум", 1073, updater
		UpdateUTFieldValue UTableID, "Размер льготы ", 536.5, updater
		UpdateUTFieldValue UTableID, "Максимальный льготный доход", 1500, updater

	End If	

End Sub
'----
'
'----
Sub AddDepends(updater)
	Dim aCodes, aDeps, Code

	Set Code = workarea.Code(Workarea.GetCodeID("2001"))
	Code.percent = 8.41
	Code.Save

	aCodes = Array("2001")
	aDeps = Array("0410", "0420", "0440", "0450")

	CheckDEpends aCodes, aDeps, True, updater
End Sub
'----
'
'----
Sub updatetables(updater)
	Dim UTableID, fMonth, fValue
	Dim FrmID, FldID, NewFld, Fld

	UTableID = Workarea.GetSysUTableID("КИЗП")

	If UTableID = 0 Then
		updater.writelog "Не найдена таблица с системным кодом КИЗП (Коэффициент индексации заработной платы)"
		updater.fail = True
		Exit Sub
	End If

	fMonth = GetUTFieldIDByName2(UTableID, "Месяц/год", updater)
	If updater.fail Then Exit Sub

	fValue = GetUTFieldIDByName2(UTableID, "Процент", updater)

	If updater.fail Then Exit Sub

	SetUTFieldValue UTableID, fMonth, "Январь 2012", updater
	SetUTFieldValue UTableID, fMonth, "Февраль 2012", updater
	SetUTFieldValue UTableID, fMonth, "Март 2012", updater

	SetUTFieldValue UTableID, fValue, CCur(0.00), updater
	SetUTFieldValue UTableID, fValue, CCur(0.00), updater
	SetUTFieldValue UTableID, fValue, CCur(0.00), updater

End Sub

'----
'
'----
Sub update_alim(updater)
	If MsgBox("Изменить значение % алиментов c 25% до 30%" & vbCrLf & _
					"для всех плательщиков алиментов ?", vbQuestion + vbOKCancel, "Внимание") = vbOK Then
		update_one_ag workarea.agents, updater
	End If
End Sub
'----
'
'----
Sub update_one_ag(agents, updater)
	Dim i

	For i = 1 To agents.count
		With agents.item(i)
			If .type = 3 Then
				If .Prop(PROP_ALI) = 25 Then
					.Prop(PROP_ALI) = 30
					.Save
					updater.writelog "Изменен % алиментов для " & .Name
				End If
			End If

			If .HasChildren Then
				update_one_ag .children, updater
			End If
		End With
	Next
End Sub
'----
'
'----
