Option Explicit

Const prmName = "Номер карточки"

'----
'
'----
Sub DoDbfRas(Op)
	Dim srcName, FS, f
	Dim srcPath
	Dim rs

	srcName = CreateLibObject("redirect").getfullpath("xbasa.emt", True)

	If Not Len(srcName) > 0 Then 
		MsgBox "Шаблон для формирования DBF файла не обнаружен.", vbCritical, "Акцент 7.0"
		Exit Sub
	End If

	srcPath = Left(srcName, InStrRev(srcName, "\", Len(srcName)-1)-1)+"\"

	Set FS = CreateObject("Scripting.FileSystemObject")

	FS.CopyFile srcName, srcPath + "xbasa.dbf", True

	Set f = fs.GetFile(srcPath + "xbasa.dbf")
	If f.attributes And 1 Then f.attributes = f.attributes - 1

	Set Rs = GetDB(srcPath)
	Write2DBF Rs, Op
	rs.Close
	MsgBox "DBF файл сформирован.", vbInformation, "Акцент 7.0"
End Sub

'----
'
'----
Sub Write2DBF(ByRef Rs, Op)
	Dim i
	Dim CardNo
	Dim Rez

	For i = 1 To op.Rows
		With op.TransListR (i).Item(1)
			If .AgID <>  0 Then 
				rs.AddNew
				With .Agent

					CardNo = .Prop (PrmName)
					If Len(CardNo) > 14 Then
						MsgBox "Неправильная длина в № карточки. Ф.И.О. : " & .Name
					Else
						rs("acct_card") = CardNo
					End If

					rs("fio") 	= .Name
					If .Type = 3 Then
						rez = IIF(.vatNo = ""  ,0 , .vatNo)
					Else
						rez = .Code
					End If
				End With

				Rs("id_code") = rez 
				Rs("suma") = Round2(.Sum  , 2)
				rs.Update
			End If 
		End With
	Next
End Sub

'----
'
'----
Function GetDB(srcPath)
	Dim db,rs

	Select Case WorkArea.Application.AppType
		Case "DAO"
			Set db = WorkArea.DAODatabase
			Set rs = db.OpenRecordset("select * from [xbasa.dbf] In '" + srcPath + "' 'dBase IV;'")
		Case "OLEDB"
			Set db = CreateObject("AdoDB.Connection")
			db.Provider = "Microsoft.Jet.OLEDB.4.0"
			db.Properties("Extended properties") = "dBase IV"
			db.Properties("Data Source") = srcPath
			db.Open
			Set rs = CreateObject("AdoDb.Recordset")
			db.CursorLocation = 3
			Set rs.ActiveConnection = db
			rs.CursorType = 1
			rs.LockType = 3
			rs.Source = "xbasa.dbf"
			rs.Open
	End Select

	Set GetDB = rs
End Function
'----
'
'----
