Option Explicit

Const ENUM_DISCOUNT				= 6 ' Перечисление "Признак льготы"
Const ENUM_DISDOHOD				= 2 ' Перечисление "Признак дохода"

Sub Workarea_OnLoad
	Dim msgb
	
	Set msgb = msg

	With msgb
		.visible = True
		.write "Акцент-заработная плата версия " & app.Version, vbInformation
		.write "Типовое решение Идеал версия " & workarea.userparam("CONFIG_VERSION1"), vbInformation

		If TestExtensions() Then 
			.write "В пути доступа к файлам были внесены изменения." & _
					  "Для дальнейшей нормальной работы программы необходимо ее перезапустить.", vbCritical
			msgbox "В пути доступа к файлам были внесены изменения." & vbCrLf & _
					  "Для дальнейшей нормальной работы программы необходимо ее перезапустить.", vbInformation, "Внимание !"

		End If
	End With

End Sub


'-------
'
'-------
Function TestExtensions()
	Dim Rd
	Dim aPath
	Dim i
	Dim Mask
	Dim AddToARD
	Dim Path2Mask
	Dim ArdFile
	Dim FSO

	Mask = Array("*.bmp", "*.gif", "*.sql", "*.htm", "*.dbf", "*.xml", "*.tml", "*.ini", "*.emt", "*.backup", "*.xsl")
	Path2Mask = Array("help\images", "help\images", "scripts", "help", "dbf", "xml", "updates", "ini", "dbf", "data\Backup", "xml")
	Set RD = CreateLibObject("Redirect")
	AddToARD = ""

	For i = 0 To UBound(Mask)
		aPath =	RD.GetPathArray(Mask(i))

		If UBound(aPath) = -1 Then
			AddToARD = AddToARD & Mask(i) & "=#data\..\" & Path2Mask(i) & vbCrLf
		End If
	Next

	If AddToARD = "" Then
		TestExtensions = False
	Else
		TestExtensions = True
		Set FSO = CreateObject("Scripting.FileSystemObject")
		Set ArdFile = FSO.OpenTextFile( RD.AppPath & ArdFName(), 8 )
		ArdFile.Write(vbCrLf & AddToARD)
		ArdFile.Close
	
		Set ArdFile = Nothing
		Set FSO = Nothing
		Set RD = Nothing
	End If

End Function



Sub Workarea_AfterCreate(Kind, KindID)
	If Kind = acAgent Then
		TestAgentProps KindID
	End If
End Sub

Sub Workarea_OnChanged(Kind, KindID)
	If Kind = acAgent Then
		TestAgentProps KindID	
	End If
End Sub

Sub TestAgentProps(KindID)
	Dim i
	Dim EnumLGID
	Dim EnumDID
	Dim EnumLGChildID

	With WorkArea.Agent(KindID)
		If .Type = 3 Then
			If .Prop("Признак дохода") = 0 Then

				EnumLGID = workarea.enum(ENUM_DISCOUNT	).ItemID(1)
				EnumDID = workarea.enum(ENUM_DISDOHOD).ItemID(1)
				EnumLGChildID = workarea.enum(ENUM_DISCOUNT	).ItemID(1)

				With .Properties
					For i = 1 To .Count
						With .Item(i)
							If .Name = "Признак дохода" Then	.SetValue EnumDID
							If .Name = "Признак льготы" Then	.SetValue EnumLGID
							If .Name = "Признак льготы на детей" Then	.SetValue EnumLGChildID
							If .Name = "ИНН" Then	.SetValue String(10, "Х")
						End With
					Next
				End With
			End If
		End If
	End With
End Sub

'-------
' Function ArdFName - вернуть имя ard-файла для текущего приложения
'-------
Function ArdFName
	Dim ver
	Select Case Left( App.Version, 3 )
	Case "6.0"
		ver = "6"
	Case "7.0"
		ver = "7"
	Case "7.4"
		ver = "74"
	End Select
	ArdFName = App.AppName & ver & Iif( App.AppType = "DAO", "", "s" ) & ".ard"
End Function
