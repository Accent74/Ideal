'==============================================
'= Акцент 7.0 
'= Создан : 28/12/2010 17:30:56
'= Автор  : admin
'= Код    : 2400 НДФЛ
'= Метод  : НДФЛ
'==============================================
Sub Method_OnCalc(Tr)
	Dim TaxSum, PrevTaxSum
	Dim PayAche, PayRoll
	Dim ES

	With Tr
		' исключаем сумму налога, начисленного на аванс
		PrevTaxSum = GetCodeSum(.DependsW, "2400а")

		.Tariff = .DependsW.SumIN + PrevTaxSum

		If .Tariff > .Sum3 Then

			PayAche = GetCodeSumW(.Depends, "0046")
			PayRoll = .Tariff - PayAche

			If PayAche = 0 Then
				TaxSum = Round2(.Sum3 * 0.964 * 0.15, 2)
				TaxSum = TaxSum + Round2((.Tariff - .Sum3) * 0.964 * 0.17, 2)
			ElseIf PayRoll = 0 Then
				TaxSum = Round2(.Sum3 * 0.98 * 0.15, 2)
				TaxSum = TaxSum + Round2((.Tariff - .Sum3) * 0.98 * 0.17, 2)
			Else
				TaxSum = Round2(PayRoll * 0.964 * 0.15, 2)
				TaxSum = TaxSum + Round2((PayAche - .Tariff + .Sum3) * 0.98 * 0.15, 2)
				TaxSum = TaxSum + Round2((.Tariff - .Sum3) * 0.98 * 0.17, 2)
			End If

		Else
			TaxSum = Round2((.DependsW.Sum + PrevTaxSum) * 0.15, 2)
		End If

		.Sum = TaxSum - PrevTaxSum - .LookUp.SumWD
		If .Sum < 0 Then .Sum = 0
	End With
End Sub
'---
'
'---
Function GetCodeSumW(Deps, Code)
	Dim i, CodeDeps, SumW

	CodeID = Workarea.GetCodeID(Code)
	SumW = 0

	If CodeID <> 0 Then
	
		Set CodeDeps = Workarea.Code(CodeID).Depends

		For i = 1 To CodeDeps.Count
			SumW = SumW + GetCodeSum(deps, CodeDeps.Item(i).Code)
		Next
	End If

	GetCodeSumW = SumW

End Function
'---
'
'---
Function GetCodeSum(deps, Code)
	Dim i, CodeSum

	CodeSum = 0

	For i = 1 To Deps.Count
		With Deps.Item(i)
			If .Code.Code = Code Then
				CodeSum = CodeSum + .Sum
			End If
		End With
	Next
	
	GetCodeSum = CodeSum
End Function
'---
'
'---
Sub Method_OnApply(Tr)
	With Tr
		If .AgID = 0 Or .Applysource = "Sum3" Then Exit Sub

		.Sum3 = WorkArea.UTable(USRTBL_LGT).GetValue(1,USRTBL_MAX_TAX,2)
		
	End With
End Sub
'---
'
'---

