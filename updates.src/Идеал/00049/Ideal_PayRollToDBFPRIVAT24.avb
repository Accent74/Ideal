Option Explicit

'#include "definition.avb"

'----
'
'----
Sub DoDbfRas(Op)
	Dim rs
	Dim srcName, FS, f
	Dim srcPath

	srcName = CreateLibObject("redirect").getfullpath("Privat24.emt", True)

	If Not Len(srcName) > 0 Then 
		MsgBox "Шаблон для формирования DBF файла не обнаружен.", vbCritical, "Акцент 7.0"
		Exit Sub
	End If

	srcPath = Left(srcName, InStrRev(srcName, "\", Len(srcName)-1)-1)+"\"

	Set FS = CreateObject("Scripting.FileSystemObject")
	FS.CopyFile srcName, srcPath+"Privat24.dbf", True

	Set f = fs.GetFile(srcPath + "Privat24.dbf")

	If f.attributes And 1 Then f.attributes = f.attributes - 1

	Set Rs = GetDB(srcPath)
	Write2DBF Rs, Op
	rs.Close
	
	MsgBox "DBF файл сформирован.", vbInformation, "Акцент 7.0"

End Sub

'----
'
'----
Sub Write2DBF(ByRef Rs, Op)
	Dim i
	Dim rez
	Dim Tbl, str1, str2

	Set Tbl = WorkArea.UTable(USRTBL_DOP)
	str1 = Tbl.GetValue (1, "Карточный бранч РП", 2)
	str2 = Tbl.GetValue (1, "Код зарплатного проекта", 2)

	For i = 1 To op.Rows
		With op.TransListR (i).Item(1)
			If .AgID <>  0 Then 
				rs.AddNew
		
				With .Agent
					If .Type = 3 Then
						rez = IIF(Not IsNumeric (.vatNo), 0, .vatNo)
					Else
						rez = .Code
					End If
					rs("LSTBL") = rez
					rs("INN") = rez
					rs("FAM") 	= Trim ( .Prop ("Фамилия") )
					rs("NAME")	= Trim ( .Prop ("Имя") )
					rs("OT")	 	= Trim ( .Prop ("Отчество") )
				End With

				rs("BRANCH") =	str1
				rs("ZPKOD") =	str2
				rs("RLSUM") =	Left(CStr(.Sum) ,7 )
				rs.Update
			End If 
		End With
	Next

End Sub

'----
'
'----
Function GetDB(srcPath)
	Dim db,rs

	Select Case WorkArea.Application.AppType
		Case "DAO"
			Set db = WorkArea.DAODatabase
			Set rs = db.OpenRecordset("select * from [Privat24.dbf] In '" + srcPath + "' 'dBase IV;'")
		Case "OLEDB"
			Set db = CreateObject("AdoDB.Connection")
			db.Provider = "Microsoft.Jet.OLEDB.4.0"
			db.Properties("Extended properties") = "dBase IV"
			db.Properties("Data Source") = srcPath
			db.Open
			Set rs = CreateObject("AdoDb.Recordset")
			db.CursorLocation = 3
			Set rs.ActiveConnection = db
			rs.CursorType = 1
			rs.LockType = 3
			rs.Source = "Privat24.dbf"
			rs.Open
	End Select

	Set GetDB = rs
End Function

'----
'
'----
