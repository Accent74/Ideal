'-----------
'	Идеал 071
'	модуль обновления 
'-----------
Option Explicit

'#include "_ideal_update_func.avb"
'#include "definition.avb"

Const CURR_YEAR = 2013

Class UpdateLog
	Public LogFileName
	Public fail
	Private LogFile
	Private IsLogfileOpened

	Sub Class_Initialize
		LogFileName = "c:\accent.log"
		IsLogfileOpened = False
		fail = False
	End Sub

	Sub Class_Terminate
		If IsLogfileOpened Then LogFile.Close
	End Sub

	Sub Writelog(TextLog)
		If Not IsLogfileOpened Then OpenFile LogFileName
		If IsLogfileOpened Then LogFile.WriteLine TextLog
	End Sub

	Sub OpenFile(FileName)
		Dim FSO

		If FileName = "" Then Exit Sub
		Set FSO = CreateObject("Scripting.FileSystemObject")
		Set LogFile = FSO.CreateTextFile(FileName, True)
		
		IsLogfileOpened = Not (LogFile Is Nothing)

	End Sub

End Class

'-------------------
'
'-------------------
Sub Main(Updater)

	SetNewTax updater
	updatetables updater
	update_time_table

	If Not updater.fail Then 
		If MsgBox("Показать что нового в программе ?", vbInformation + vbOKCancel, "Акцент-заработная плата Идеал") = vbOK Then
			ShowWhatsNew "WhatNewIdeal.chm", "071.htm"
		End If
	Else
		MsgBox "В процессе установки обновления произошли ошибки." & vbNewLine & _
					"Смотрите в log-файле более детальную информацию.", vbCritical, "Акцент. Установка обновлений"
	End If

End Sub
'----
'
'----
Sub SetNewTax(updater)
	Dim UTableID

	If MsgBox("Обновлять ставки c января 2013 года ?", vbQuestion + vbOKCancel, "Акцент " & App.Version) = vbOK Then
		UTableID = Workarea.GetSysUTableID("ПДТ")

		If UTableID = 0 Then
			updater.writelog "Не найдена таблица с системным кодом ПДТ (Подоходный налог)"
			updater.fail = True
			Exit Sub
		End If
	
		UpdateUTFieldValue UTableID, "Максимальный доход", 19499, updater
		UpdateUTFieldValue UTableID, "Прожиточный минимум", 1147, updater
		UpdateUTFieldValue UTableID, "Размер льготы", 573.50, updater
		UpdateUTFieldValue UTableID, "Максимальный льготный доход", 1610, updater
		UpdateUTFieldValue UTableID, "Максимальный доход для НДФЛ", 11470, updater

	End If	
End Sub

'----
'
'----
Sub updatetables(updater)
	Dim UTableID, fMonth, fValue
	Dim FrmID, FldID, NewFld, Fld

	UTableID = Workarea.GetSysUTableID("КИЗП")

	If UTableID = 0 Then
		updater.writelog "Не найдена таблица с системным кодом КИЗП (Коэффициент индексации заработной платы)"
		updater.fail = True
		Exit Sub
	End If

	fMonth = GetUTFieldIDByName2(UTableID, "Месяц/год", updater)
	If updater.fail Then Exit Sub

	fValue = GetUTFieldIDByName2(UTableID, "Процент", updater)

	If updater.fail Then Exit Sub

	SetUTFieldValue UTableID, fMonth, "Январь 2013", updater
	SetUTFieldValue UTableID, fMonth, "Февраль 2013", updater
	SetUTFieldValue UTableID, fMonth, "Март 2013", updater

	SetUTFieldValue UTableID, fValue, CCur(0.00), updater
	SetUTFieldValue UTableID, fValue, CCur(0.00), updater
	SetUTFieldValue UTableID, fValue, CCur(0.00), updater

End Sub

'----
'
'----
Sub update_time_table
	Dim i, SQL1, SQL2, SQL3
	Dim Days, Hours, pp_id
	Dim DefTableID
	
	Days = Array(21, 20, 20, 22, 19, 18, 23, 21, 21, 23, 21, 22)
	Hours = Array(168,160,159,175,151,143,184,167,168,184,168,175)
	
	SQL1 = "insert into p_period (pp_type, pp_month, pp_year) values (0, "
	SQL2 = "insert into time_table_values (tt_id, pp_id, ttv_days, TTV_HOURS) values ("
	
	DefTableID = GetDefTable()
	
	For i = 1 To 12
		If FindPeriod(i, CURR_YEAR) = 0 Then
			If App.AppType = "DAO" Then
				workarea.daodatabase.execute SQL1 & i & ", " & CURR_YEAR & ")"
			Else
				RunSQL SQL1 & i & ", " & CURR_YEAR & ")"
			End If

			pp_id = FindPeriod(i, CURR_YEAR)

			If App.AppType = "DAO" Then
				workarea.daodatabase.execute SQL2 & DefTableID & ", " & pp_id & ", " & Days(i-1) * 10 & "," & Hours(i-1) * 10 & ")"
			Else
				RunSQL SQL2 & DefTableID & ", " & pp_id & ", " & Days(i-1) * 10 & "," & Hours(i-1) * 10 & ")"
			End If
		End If
	Next
	
	SQL3 = "insert into time_table_holidays (tth_year, tth_1, tth_2, tth_3, tth_4, tth_5, tth_6, tth_7, tth_8, tth_9, tth_10, tth_11, tth_12) values ("
	SQL3 = SQL3 & CURR_YEAR & ",	65,	0,	128,	0,	275,	138412032,	0,	8388608,	0,	0,	0,	0)"
	
	If Not find_holidays(CURR_YEAR) Then
		If app.appType = "DAO" Then
			workarea.daodatabase.execute SQL3
		Else
			RunSQL SQL3
		End If
	End If
End Sub
'----
'
'----
Function FindPeriod(nMonth, nYear)
	Dim Rs, SQL

	SQL = "select pp_id from p_period where pp_month=" & nMonth & " and pp_year=" & nYear

	If App.AppType = "DAO" Then
		Set Rs = Workarea.DAODatabase.Openrecordset(SQL)
	Else
		Set Rs = RunSQL(SQL)
	End If

	If Rs.eof Then 
		FindPeriod = 0
	Else
		FindPeriod = rs.fields(0).value
	End If
End Function
'----
'
'----
Function find_holidays(nYear)
	Dim Rs, SQL

	SQL = "select tth_year from time_table_holidays where tth_year=" & nYear

	If App.AppType = "DAO" Then
		Set Rs = workarea.daodatabase.openrecordset(SQL)
	Else
		Set RS = RunSQL(SQL)
	End If

	find_holidays = Not Rs.eof

End Function
'----
'
'----
Function GetDefTable()
	Dim Rs, SQL

	SQL = "select tt_id from time_tables where tt_flags = 1"

	If App.AppType = "DAO" Then
		Set Rs = workarea.daodatabase.openrecordset(SQL)
	Else
		Set Rs = RunSQL(SQL)
	End If

	If Rs.eof Then
		GetDefTable = 0
	Else
		GetDefTable = Rs.Fields(0).Value
	End If
End Function
'----
'
'----
Function RunSQL(SQL)
	Dim Cn, Cmd

	Set Cn = WorkArea.AdoConnection
	Cn.CursorLocation = 2 'курсор на нашей стороне
	Set Cmd = CreateObject("ADODB.Command")
	Set Cmd.ActiveConnection = Cn 
	Cmd.CommandText = SQL
	Set RunSQL = 	Cmd.Execute	' выполняем команду 

End Function
'----
'
'----
