'-----------
'	Идеал 099
'	модуль обновления 
'-----------
Option Explicit

'#include "_ideal_update_func.avb"

Const CURR_YEAR = 2015

Class UpdateLog
	Public LogFileName
	Public fail
	Private LogFile
	Private IsLogfileOpened

	Sub Class_Initialize
		LogFileName = "d:\accent.log"
		IsLogfileOpened = False
		fail = False
	End Sub

	Sub Class_Terminate
		If IsLogfileOpened Then LogFile.Close
	End Sub

	Sub Writelog(TextLog)
		If Not IsLogfileOpened Then OpenFile LogFileName
		If IsLogfileOpened Then LogFile.WriteLine TextLog
	End Sub

	Sub OpenFile(FileName)
		Dim FSO

		If FileName = "" Then Exit Sub
		Set FSO = CreateObject("Scripting.FileSystemObject")
		Set LogFile = FSO.CreateTextFile(FileName, True)
		
		IsLogfileOpened = Not (LogFile Is Nothing)
	End Sub

	Public Sub Refresh
	End Sub

End Class

'Dim updater
'Set updater = New UpdateLog
'stop
'Main Updater
'-------------------
'
'-------------------
Sub Main(Updater)

	SetNewTax updater
	If Not updater.fail Then addcode updater

End Sub
'----
'
'----
Sub addcode(updater)
	Dim Fld, NewCodeRoot

	If Workarea.GetCodeID("0022") = 0 Then
		Set Fld = GetFolder("Отчеты", Workarea.Codes)
		If Not Fld Is Nothing Then
			Set NewCodeRoot = Fld.Children.Create(103, "0022", "Уважительные причины")

			If Not NewCodeRoot Is Nothing Then
				updater.writelog "Добавлен код 0022 'Уважительные причины'"
				NewCodeRoot.AddDependency Workarea.GetCodeID("8001")

			Else
				updater.writelog "Невозможно добавить код 0022 'Уважительные причины'"
				updater.fail = True
			End If
		Else
			updater.writelog "Папка 'Отчеты' в списке кодов начислений/удержаний не найдена"
			updater.fail = True
		End If
	End If

End Sub
'----
'
'----
Sub SetNewTax(updater)
	Dim UTableID

	UTableID = Workarea.GetSysUTableID("ПДТ")

	If UTableID = 0 Then
		updater.writelog "Не найдена таблица с системным кодом ПДТ (Подоходный налог)"
		updater.fail = True
		Exit Sub
	End If
	
	UpdateUTFieldValue UTableID, "Прожиточный минимум", 1218.00, updater

End Sub
'----
'
'----
