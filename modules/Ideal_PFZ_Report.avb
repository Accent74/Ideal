Option Explicit

' mapHead - Значения полей заголовка отчёта
' выборка значений делается при формировании заголовка
' должен быть глобальным. Потому что используется также при формировании имени файла
Dim mapHead

'________________________________________________
Sub CreateXMLReport(Sh, PFCode, MCName, MCCode)
	Dim fname, path
	Dim arXML( 1, 10 )

	Set mapHead = CreateLibObject( "Map" )

	GenerateXML Sh, arXML, PFCode, MCName, MCCode
	path = GetLastPath
	fname = GetXMLFName_PFZ & ".pfz"

	If path = "" Then
		If GetFileName_PFZ( path, fname ) Then
			' формируем pfz-пакет
			SaveXML path, fname, arXML, Sh
		End If
	Else
		Select Case MsgBox( "Хотите сохранить файл в папку '" & path & "'?" & vbNewLine & "Да  - отчёт будет сохранён в указанную папку." _
				& vbNewLine & "Нет - будет выдано окно, позволяющее сохранить отчёт в любую выбранную вами папку." & vbNewLine & _
				"Отмена - сохранение файла отчёта не произойдёт.", vbYesNoCancel + vbQuestion, "Отчёт сформирован. Куда записать файл?" )
			Case vbYes
				' формируем pfz-пакет
				SaveXML path, fname, arXML, Sh
			Case vbNo
				If GetFileName_PFZ( path, fname ) Then
					' формируем pfz-пакет
					SaveXML path, fname, arXML, Sh
				End If
		End Select
	End If
End Sub

'________________________________________________
Function GenerateXML( Sh, ByRef arXML, PFCode, MCName, MCCode )
	Dim i, xml

	GenerateTitle xml, ShtBook.Sheet( 1 ), PFCode, MCName, MCCode
	Set arXML( 1, 0 ) = xml
	Set xml = Nothing
	arXML( 0, 0 ) = "P04T00B.xml"

	GenerateTable5 xml, ShtBook.Sheet( 2 ), PFCode, MCName, MCCode
	Set arXML( 1, 5 ) = xml
	Set xml = Nothing
	arXML( 0, 5 ) = "P04T05B.xml"

	GenerateTable6 xml, ShtBook.Sheet( 3 ), PFCode, MCName, MCCode
	Set arXML( 1, 6 ) = xml
	Set xml = Nothing
	arXML( 0, 6 ) = "P04T06B.xml"

	GenerateTable7 xml, ShtBook.Sheet( 4 ), PFCode, MCName, MCCode
	Set arXML( 1, 7 ) = xml
	Set xml = Nothing
	arXML( 0, 7 ) = "P04T07B.xml"

	GenerateTable8 xml, ShtBook.Sheet( 5 ), PFCode, MCName, MCCode
	Set arXML( 1, 8 ) = xml
	Set xml = Nothing
	arXML( 0, 8 ) = "P04T08B.xml"

'	For i = 1 To UBound( arTables )
'		If arTables( 0, i ) <> 0 Then
'	Next
End Function

'________________________________________________
Sub SaveXML( path, fname, arXML, Sh )
	Dim i, path_tables
	Dim FSO

	path_tables = path & "_tables\"
	Set FSO = CreateObject( "Scripting.FileSystemObject" )

	If FSO.FolderExists( path_tables ) Then
		' очищаем папку от старых файлов
		FSO.DeleteFolder Left( path_tables, Len( path_tables ) - 1 ), True
	End If

	FSO.CreateFolder path_tables

	For i = 0 To UBound( arXML, 2 )
		If Not IsEmpty(arXML( 1, i )) Then
			arXML( 1, i ).save path_tables & arXML( 0, i )
		End If
	Next

	' формируем pfz-пакет
	If CreatePFZ( path_tables, path, fname ) Then
		MsgBox "Пакет сохранён в файл: " & fname & vbNewLine & "В папке: " & path, vbInformation
	End If

	FSO.DeleteFolder Left( path_tables, Len( path_tables ) - 1 ), True

End Sub

'________________________________________________
' генерируем "Титульную страницу = Ярлык"
Function GenerateTitle( ByRef DOMDoc, sht, PFCode, MCName, MCCode )
	Dim xmlDECLAR, xml, xmlParent

	Set DOMDoc = CreateObject( "Msxml2.DOMDocument" )
	Set xml = DOMDoc.createProcessingInstruction( "xml", "version='1.0' encoding='windows-1251'" )
	DOMDoc.AppendChild xml

	' Генерируем элемент DECLAR, состоящий из DHead & DBody
	Set xmlDECLAR = DOMDoc.createElement( "DECLAR" )
	DOMDoc.AppendChild xmlDECLAR

	xmlDECLAR.SetAttribute "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance"
	xmlDECLAR.SetAttribute "xsi:noNamespaceSchemaLocation", "P04T00B.xsd"

	'_______________________________________________
	' Заголовок отчёта
	Set xml = DOMDoc.CreateElement( "DECLARHEAD" )
	xmlDECLAR.AppendChild xml

	' "кэшируем" вычисленные значения
	mapHead( "VER" ) = AppendNew( xml, "VER", "1.1" )
	mapHead( "C_DOC" ) = AppendNew( xml, "C_DOC", "P04" )
	mapHead( "C_PFU" ) = AppendNew( xml, "C_PFU", PFCode )
	mapHead( "SOFTWARE" ) = AppendNew( xml, "SOFTWARE", App.AppName & Space( 1 ) & App.AppType & Space( 1 ) & App.Version )
	mapHead( "TIN" ) = AppendNew( xml, "TIN", MCCode )
	mapHead( "PERIOD_MONTH" ) = AppendNew( xml, "PERIOD_MONTH", ShtBook.CalcFormula( "Period1.Month" ))
	mapHead( "PERIOD_YEAR" ) = AppendNew( xml, "PERIOD_YEAR", ShtBook.CalcFormula( "Period1.Year" ))
	mapHead( "DATA_FILL" ) = AppendNew( xml, "DATA_FILL", Date2Num( ShtBook.Map( "FILL_DATE" )))
	AppendNew xml, "ID_OUT", ""

	'_______________________________________________
	' Тело отчёта
	Set xmlParent = DOMDoc.CreateElement( "DECLARBODY" )
	xmlDECLAR.AppendChild xmlParent

	Set xml = DOMDoc.CreateElement( "PAGE" )
	Set xmlParent = xmlParent.AppendChild( xml )

	Set xml = DOMDoc.CreateElement( "HEADER" )
	xmlParent.AppendChild xml

	AppendNew xml, "FIRM_EDRPOU", mapHead( "TIN" )
	AppendNew xml, "FIRM_FIL", ""
	AppendNew xml, "FIRM_NAME", MCName

	Set xml = DOMDoc.CreateElement( "TABLE" )
	xmlParent.AppendChild xml

	AppendNew xml, "N1_0", ToInt( sht.Cell( 16, 6 ).Value )
	AppendNew xml, "N2_0", ToInt( sht.Cell( 17, 6 ).Value )
	AppendNew xml, "N3_0", ToInt( sht.Cell( 18, 6 ).Value )
	AppendNew xml, "N4_0", ToInt( sht.Cell( 19, 6 ).Value )

	AppendNew xml, "N5_0", ToInt( sht.Cell( 20, 6 ).Value )
	AppendNew xml, "N5_1", ToInt( sht.Cell( 20, 7 ).Value )
	AppendNew xml, "N6_0", ToInt( sht.Cell( 21, 6 ).Value )
	AppendNew xml, "N6_1", ToInt( sht.Cell( 21, 7 ).Value )
	AppendNew xml, "N7_0", ToInt( sht.Cell( 22, 6 ).Value )
	AppendNew xml, "N7_1", ToInt( sht.Cell( 22, 7 ).Value )
	AppendNew xml, "N8_0", ToInt( sht.Cell( 23, 6 ).Value )
	AppendNew xml, "N8_1", ToInt( sht.Cell( 23, 7 ).Value )
	AppendNew xml, "N9_0", ToInt( sht.Cell( 24, 6 ).Value )
	AppendNew xml, "N9_1", ToInt( sht.Cell( 24, 7 ).Value )
	AppendNew xml, "N10_0", ToInt( sht.Cell( 25, 6 ).Value )
	AppendNew xml, "N10_1", ToInt( sht.Cell( 25, 7 ).Value )
	AppendNew xml, "N11_0", ToInt( sht.Cell( 26, 6 ).Value )
	AppendNew xml, "N11_1", ToInt( sht.Cell( 26, 7 ).Value )

	Set xml = DOMDoc.CreateElement( "FOOTER" )
	xmlParent.AppendChild xml

	mapHead( "BOSS_NUMIDENT" ) = AppendNew( xml, "BOSS_NUMIDENT", sht.Cell( 28, 3 ).Value )
	mapHead( "FIRM_BOSS" ) = AppendNew( xml, "FIRM_BOSS", sht.Cell( 28, 5 ).Value )
	mapHead( "BUH_NUMIDENT" ) = AppendNew( xml, "BUH_NUMIDENT", sht.Cell( 31, 3 ).Value )
	mapHead( "FIRM_BUH" ) = AppendNew( xml, "FIRM_BUH", sht.Cell( 31, 5 ).Value )

	GenerateTitle = True
End Function


'________________________________________________
' генерируем Заголовок таблицы Х
Sub GenerateDHead( ByRef DOMDoc, ByRef xmlDECLAR )
	Dim xml

	Set xml = DOMDoc.CreateElement( "DECLARHEAD" )
	xmlDECLAR.AppendChild xml

	' "кэшируем" вычисленные значения
	AppendNew xml, "VER", mapHead( "VER" )
	AppendNew xml, "C_DOC", mapHead( "C_DOC" )
	AppendNew xml, "C_PFU", mapHead( "C_PFU" )
	AppendNew xml, "SOFTWARE", mapHead( "SOFTWARE" )
	AppendNew xml, "TIN", mapHead( "TIN" )
	AppendNew xml, "PERIOD_MONTH",  mapHead( "PERIOD_MONTH" )
	AppendNew xml, "PERIOD_YEAR", mapHead( "PERIOD_YEAR" )
	AppendNew xml, "DATA_FILL", mapHead( "DATA_FILL" )
	AppendNew xml, "ID_OUT", ""
End Sub

'________________________________________________
' генерируем "Таблиця 5. Трудові відносини застрахованих осіб"
Function GenerateTable5( ByRef DOMDoc, sht, PFCode, MCName, MCCode )
	Dim xmlDECLAR, xml, xmlParent
	Dim row, a

	Set DOMDoc = CreateObject( "Msxml2.DOMDocument" )
	Set xml = DOMDoc.createProcessingInstruction( "xml", "version='1.0' encoding='windows-1251'" )
	DOMDoc.AppendChild xml

	' Генерируем элемент DECLAR, состоящий из DHead & DBody
	Set xmlDECLAR = DOMDoc.createElement( "DECLAR" )
	DOMDoc.AppendChild xmlDECLAR

	xmlDECLAR.SetAttribute "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance"
	xmlDECLAR.SetAttribute "xsi:noNamespaceSchemaLocation", "P04T06B.xsd"

	'_______________________________________________
	' Заголовок отчёта
	GenerateDHead DOMDoc, xmlDECLAR

	'_______________________________________________
	' Тело отчёта
	Set xmlParent = DOMDoc.CreateElement( "DECLARBODY" )
	xmlDECLAR.AppendChild xmlParent

	Set xml = DOMDoc.CreateElement( "PAGE" )
	Set xmlParent = xmlParent.AppendChild( xml )

	Set xml = DOMDoc.CreateElement( "HEADER" )
	xmlParent.AppendChild xml

	AppendNew xml, "PAGE_NUM", 1
	AppendNew xml, "FIRM_EDRPOU", mapHead( "TIN" )
	AppendNew xml, "FIRM_FIL", ""

	If Len( Trim( sht.Cell( 8, 3 ).Value )) <> 0 Then
		a = 1
	ElseIf Len( Trim( sht.Cell( 8, 5 ).Value )) <> 0 Then
		a = 2
	Else
		a = 3
	End If

	AppendNew xml, "FORM_TYPE", CStr( a )
	AppendNew xml, "FIRM_NAME", MCName

	Set xml = DOMDoc.CreateElement( "TABLE" )
	Set xmlParent = xmlParent.AppendChild( xml )

	Dim LastColumn, TotalRows
	Dim tblRows

	LastColumn = sht.Columns
	TotalRows = sht.Rows

	For row = 13 To TotalRows
		tblRows = tblRows + 1

		Set xml = DOMDoc.CreateElement( "ROW" )
		xmlParent.AppendChild xml

		AppendNew xml, "ROWNUM", sht.Cell( row, 1 ).Value
		AppendNew xml, "NUMIDENT", sht.Cell( row, 3 ).Value
		AppendNew xml, "LN", sht.Cell( row, 4 ).Value
		AppendNew xml, "NM", sht.Cell( row, 5 ).Value
		AppendNew xml, "FTN", sht.Cell( row, 6 ).Value
		AppendNew xml, "START_DT", ToInt( sht.Cell( row, 7 ).Value )
		AppendNew xml, "END_DT", ToInt( sht.Cell( row, 8 ).Value )
	Next

	Set xml = DOMDoc.CreateElement( "FOOTER" )
	Set xmlParent = xmlParent.parentNode
	xmlParent.AppendChild xml

	AppendNew xml, "ROWS", TotalRows - row
	AppendNew xml, "BOSS_NUMIDENT", mapHead( "BOSS_NUMIDENT" )
	AppendNew xml, "FIRM_BOSS", mapHead( "FIRM_BOSS" )
	AppendNew xml, "BUH_NUMIDENT", mapHead( "BUH_NUMIDENT" )
	AppendNew xml, "FIRM_BUH", mapHead( "FIRM_BUH" )
End Function

'________________________________________________
' генерируем "Таблиця 6. Відомості про нарахування заробітку(доходу) застрахованим особам"
Function GenerateTable6( ByRef DOMDoc, sht, PFCode, MCName, MCCode )
	Dim xmlDECLAR, xml, xmlParent
	Dim row

	Set DOMDoc = CreateObject( "Msxml2.DOMDocument" )
	Set xml = DOMDoc.createProcessingInstruction( "xml", "version='1.0' encoding='windows-1251'" )
	DOMDoc.AppendChild xml

	' Генерируем элемент DECLAR, состоящий из DHead & DBody
	Set xmlDECLAR = DOMDoc.createElement( "DECLAR" )
	DOMDoc.AppendChild xmlDECLAR

	xmlDECLAR.SetAttribute "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance"
	xmlDECLAR.SetAttribute "xsi:noNamespaceSchemaLocation", "P04T06B.xsd"

	'_______________________________________________
	' Заголовок отчёта
	GenerateDHead DOMDoc, xmlDECLAR

	'_______________________________________________
	' Тело отчёта
	Set xmlParent = DOMDoc.CreateElement( "DECLARBODY" )
	xmlDECLAR.AppendChild xmlParent

	Set xml = DOMDoc.CreateElement( "PAGE" )
	Set xmlParent = xmlParent.AppendChild( xml )

	Set xml = DOMDoc.CreateElement( "HEADER" )
	xmlParent.AppendChild xml

	AppendNew xml, "FIRM_EDRPOU", mapHead( "TIN" )
	AppendNew xml, "FIRM_FIL", ""
	AppendNew xml, "FORM_TYPE", Iif( Len( Trim( sht.Cell( 8, 10 ).Value )) <> 0, 1, 3 )
	AppendNew xml, "FIRM_NAME", MCName
	AppendNew xml, "PAGE_NUM", 1

	Set xml = DOMDoc.CreateElement( "TABLE" )
	Set xmlParent = xmlParent.AppendChild( xml )

	Dim LastColumn, TotalRows
	Dim tblRows

	LastColumn = sht.Columns
	TotalRows = sht.Rows

	For row = 12 To TotalRows
		Set xml = DOMDoc.CreateElement( "ROW" )
		xmlParent.AppendChild xml

		AppendNew xml, "ROWNUM", sht.Cell( row, 1 ).Value
		AppendNew xml, "NUMIDENT", sht.Cell( row, 3 ).Value
		AppendNew xml, "ZO", sht.Cell( row, 7 ).Value
		AppendNew xml, "ZIC", sht.Cell( row, 8 ).Value
		AppendNew xml, "LN", sht.Cell( row, 4 ).Value
		AppendNew xml, "NM", sht.Cell( row, 5 ).Value
		AppendNew xml, "FTN", sht.Cell( row, 6 ).Value
		AppendNew xml, "SUM_TOTAL", ToMoney( sht.Cell( row, 9 ).Value )
		AppendNew xml, "SUM_MAX", ToMoney( sht.Cell( row, 10 ).Value )
		AppendNew xml, "SUM_INS", ToMoney( sht.Cell( row, 11 ).Value )
		AppendNew xml, "OTK", ToInt( sht.Cell( row, 12 ).Value )
		AppendNew xml, "EXP", ToInt( sht.Cell( row, 13 ).Value )
	Next

	Set xml = DOMDoc.CreateElement( "FOOTER" )
	Set xmlParent = xmlParent.parentNode
	xmlParent.AppendChild xml

	AppendNew xml, "PAGE_SUM_TOTAL", ToMoney( sht.Cell( sht.rows, 9 ).Value )
	AppendNew xml, "PAGE_SUM_MAX", ToMoney( sht.Cell( sht.rows, 10 ).Value )
	AppendNew xml, "PAGE_SUM_INS", ToMoney( sht.Cell( sht.rows, 11 ).Value )
	AppendNew xml, "ROWS", TotalRows - row
	AppendNew xml, "BOSS_NUMIDENT", mapHead( "BOSS_NUMIDENT" )
	AppendNew xml, "FIRM_BOSS", mapHead( "FIRM_BOSS" )
	AppendNew xml, "BUH_NUMIDENT", mapHead( "BUH_NUMIDENT" )
	AppendNew xml, "FIRM_BUH", mapHead( "FIRM_BUH" )
End Function

'________________________________________________
' генерируем "Таблиця 7. Відомості про нарахування заробітку(доходу) застрахованим особам"
Function GenerateTable7( ByRef DOMDoc, sht, PFCode, MCName, MCCode)
	Dim xmlDECLAR, xml, xmlParent
	Dim row

	Set DOMDoc = CreateObject( "Msxml2.DOMDocument" )
	Set xml = DOMDoc.createProcessingInstruction( "xml", "version='1.0' encoding='windows-1251'" )
	DOMDoc.AppendChild xml

	' Генерируем элемент DECLAR, состоящий из DHead & DBody
	Set xmlDECLAR = DOMDoc.createElement( "DECLAR" )
	DOMDoc.AppendChild xmlDECLAR

	xmlDECLAR.SetAttribute "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance"
	xmlDECLAR.SetAttribute "xsi:noNamespaceSchemaLocation", "P04T07B.xsd"

	'_______________________________________________
	' Заголовок отчёта
	GenerateDHead DOMDoc, xmlDECLAR

	'_______________________________________________
	' Тело отчёта
	Set xmlParent = DOMDoc.CreateElement( "DECLARBODY" )
	xmlDECLAR.AppendChild xmlParent

	Set xml = DOMDoc.CreateElement( "PAGE" )
	Set xmlParent = xmlParent.AppendChild( xml )

	Set xml = DOMDoc.CreateElement( "HEADER" )
	xmlParent.AppendChild xml

	AppendNew xml, "FIRM_EDRPOU", mapHead( "TIN" )
	AppendNew xml, "FIRM_FIL", ""
	AppendNew xml, "FORM_TYPE", Iif( Len( Trim( sht.Cell( 8, 10 ).Value )) <> 0, 1, 3 )
	AppendNew xml, "FIRM_NAME", MCName
	AppendNew xml, "PAGE_NUM", 1

	Set xml = DOMDoc.CreateElement( "TABLE" )
	Set xmlParent = xmlParent.AppendChild( xml )

	Dim LastColumn, TotalRows
	Dim tblRows

	LastColumn = sht.Columns
	TotalRows = sht.Rows

	For row = 12 To TotalRows
		Set xml = DOMDoc.CreateElement( "ROW" )
		xmlParent.AppendChild xml

		AppendNew xml, "ROWNUM", sht.Cell( row, 1 ).Value
		AppendNew xml, "NUMIDENT", sht.Cell( row, 3 ).Value
		AppendNew xml, "ZO", sht.Cell( row, 7 ).Value
		AppendNew xml, "ZIC", sht.Cell( row, 8 ).Value
		AppendNew xml, "PAY_MNTH", ToInt( sht.Cell( row, 9 ).Value )
		AppendNew xml, "PAY_YEAR", ToInt( sht.Cell( row, 10 ).Value )
		AppendNew xml, "PAY_TP", ToInt( sht.Cell( row, 11 ).Value )
		AppendNew xml, "LN", sht.Cell( row, 4 ).Value
		AppendNew xml, "NM", sht.Cell( row, 5 ).Value
		AppendNew xml, "FTN", sht.Cell( row, 6 ).Value
		AppendNew xml, "SUM_TOTAL", ToMoney( sht.Cell( row, 12 ).Value )
		AppendNew xml, "SUM_MAX", ToMoney( sht.Cell( row, 13 ).Value )
		AppendNew xml, "SUM_INS", ToMoney( sht.Cell( row, 14 ).Value )
		AppendNew xml, "OTK", ToInt( sht.Cell( row, 15 ).Value )
		AppendNew xml, "EXP", ToInt( sht.Cell( row, 16 ).Value )
	Next

	AppendNew xmlParent, "PAGE_SUM_TOTAL", ToMoney( sht.Cell( sht.rows, 12 ).Value )
	AppendNew xmlParent, "PAGE_SUM_MAX", ToMoney( sht.Cell( sht.rows, 13 ).Value )
	AppendNew xmlParent, "PAGE_SUM_INS", ToMoney( sht.Cell( sht.rows, 14 ).Value )
	AppendNew xmlParent, "ROWS", TotalRows - row

	Set xml = DOMDoc.CreateElement( "FOOTER" )
	Set xmlParent = xmlParent.parentNode
	xmlParent.AppendChild xml

	AppendNew xml, "BOSS_NUMIDENT", mapHead( "BOSS_NUMIDENT" )
	AppendNew xml, "FIRM_BOSS", mapHead( "FIRM_BOSS" )
	AppendNew xml, "BUH_NUMIDENT", mapHead( "BUH_NUMIDENT" )
	AppendNew xml, "FIRM_BUH", mapHead( "FIRM_BUH" )
End Function

'________________________________________________
' генерируем "Таблиця 8. Наявність підстав для обліку стажу окремим категоріям осіб відповідно до законодавства"
Function GenerateTable8( ByRef DOMDoc, sht , PFCode, MCName, MCCode)
	Dim xmlDECLAR, xml, xmlParent
	Dim row

	Set DOMDoc = CreateObject( "Msxml2.DOMDocument" )
	Set xml = DOMDoc.createProcessingInstruction( "xml", "version='1.0' encoding='windows-1251'" )
	DOMDoc.AppendChild xml

	' Генерируем элемент DECLAR, состоящий из DHead & DBody
	Set xmlDECLAR = DOMDoc.createElement( "DECLAR" )
	DOMDoc.AppendChild xmlDECLAR

	xmlDECLAR.SetAttribute "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance"
	xmlDECLAR.SetAttribute "xsi:noNamespaceSchemaLocation", "P04T08B.xsd"

	'_______________________________________________
	' Заголовок отчёта
	GenerateDHead DOMDoc, xmlDECLAR

	'_______________________________________________
	' Тело отчёта
	Set xmlParent = DOMDoc.CreateElement( "DECLARBODY" )
	xmlDECLAR.AppendChild xmlParent

	Set xml = DOMDoc.CreateElement( "PAGE" )
	Set xmlParent = xmlParent.AppendChild( xml )

	Set xml = DOMDoc.CreateElement( "HEADER" )
	xmlParent.AppendChild xml

	AppendNew xml, "FIRM_EDRPOU", mapHead( "TIN" )
	AppendNew xml, "FIRM_FIL", ""
	AppendNew xml, "FORM_TYPE", Iif( Len( Trim( sht.Cell( 8, 11 ).Value )) <> 0, 1, 3 )
	AppendNew xml, "FIRM_NAME", MCName
	AppendNew xml, "PAGE_NUM", 1

	Set xml = DOMDoc.CreateElement( "TABLE" )
	Set xmlParent = xmlParent.AppendChild( xml )

	Dim LastColumn, TotalRows
	Dim tblRows

	LastColumn = sht.Columns
	TotalRows = sht.Rows

	For row = 13 To TotalRows
		Set xml = DOMDoc.CreateElement( "ROW" )
		xmlParent.AppendChild xml

		AppendNew xml, "ROWNUM", sht.Cell( row, 1 ).Value
		AppendNew xml, "NUMIDENT", sht.Cell( row, 3 ).Value
		AppendNew xml, "C_PID", sht.Cell( row, 7 ).Value
		AppendNew xml, "START_DT", StrRightJstfd( sht.Cell( row, 8 ).Value, 2, "0" )
		AppendNew xml, "STOP_DT", StrRightJstfd( sht.Cell( row, 9 ).Value, 2, "0" )
		AppendNew xml, "DAYS", ToInt( sht.Cell( row, 10 ).Value )
		AppendNew xml, "NORMZ", ToInt( sht.Cell( row, 11 ).Value )
		AppendNew xml, "HH", ToInt( sht.Cell( row, 12 ).Value )
		AppendNew xml, "MM", ToInt( sht.Cell( row, 13 ).Value )
		AppendNew xml, "NORMA", sht.Cell( row, 14 ).Value
		AppendNew xml, "SEASON", sht.Cell( row, 15 ).Value
		AppendNew xml, "LN", sht.Cell( row, 4 ).Value
		AppendNew xml, "NM", sht.Cell( row, 5 ).Value
		AppendNew xml, "FTN", sht.Cell( row, 6 ).Value
	Next

	AppendNew xmlParent, "ROWS", TotalRows - row

	Set xmlParent = xmlParent.parentNode
	Set xml = DOMDoc.CreateElement( "FOOTER" )
	xmlParent.AppendChild xml

	AppendNew xml, "BOSS_NUMIDENT", mapHead( "BOSS_NUMIDENT" )
	AppendNew xml, "FIRM_BOSS", mapHead( "FIRM_BOSS" )
	AppendNew xml, "BUH_NUMIDENT", mapHead( "BUH_NUMIDENT" )
	AppendNew xml, "FIRM_BUH", mapHead( "FIRM_BUH" )
End Function

'________________________________________________
Function AppendNew( parent, name, val )
	Dim xmlNODE

	Set xmlNODE = parent.ownerDocument.CreateElement( name )
	xmlNODE.Text = val
	parent.AppendChild xmlNODE
	AppendNew = val
End Function

'________________________________________________
' Function ToInt( num )
' Преобразовать "содержимое" ячейки к целому числу
'________________________________________________
Function ToInt( num )
	If IsNumeric( num ) Then
		ToInt = CStr( num )
	Else
		ToInt = "0"
	End If
End Function

'________________________________________________
' Function ToInt( num )
' Преобразовать "содержимое" ячейки к целому числу
'________________________________________________
Function ToMoney( num )
	If IsNumeric( num ) Then
		ToMoney = Format2( num, "#.00" )
	Else
		ToMoney = "0.00"
	End If
End Function

'________________________________________________
Function GetXMLFName_PFZ()
	GetXMLFName_PFZ = StrRightJstfd( mapHead( "C_PFU" ), 5, "0" ) & StrRightJstfd( mapHead( "TIN" ), 8, "0" ) & "ZZZZZZZ" & _
			Replace( FormatDate2( ShtBook.Map( "FILL_DATE" ) + ShtBook.Map( "FILL_TIME" ), "dd/mm/yyyy/hh/nn/ss" ), "/", "" ) & "P04B0"
End Function

'________________________________________________
Function GetLastPath
	GetLastPath = Checknull( Workarea.UserParam( "PFZ_PATH_" & CreateObject( "WScript.Network" ).ComputerName ), "" )
End Function

'________________________________________________
Function GetFileName_PFZ( ByRef path, ByRef fname )
	Dim FDlg
	Dim prmName

	Set FDlg = CreateLibObject( "FileDialog" )
	FDlg.Caption = "Выберите папку для сохранения отчёта"
	FDlg.Filter = "Пакеты ПФУ (*.pfz)|*.pfz|Все файлы (*.*)|*.*"
	FDlg.InitialDir = path
	FDlg.DefaultName = fname

	If FDlg.ShowSave = vbOK Then
		prmName = "PFZ_PATH_" & CreateObject( "WScript.Network" ).ComputerName
		fname = FDlg.FileName
		path = Left( FDlg.PathName, Len( FDlg.PathName ) - Len( FDlg.FileName ))
		Workarea.UserParam( prmName ) = path
		GetFileName_PFZ = True
	Else
		GetFileName_PFZ = False
	End If
End Function

'________________________________________________
Function CellVal( Row, Col, sht )
	Dim a
	a = Trim( ShtBook.Sheet( sht ).Cell( Row, Col ).Value )
	If a = "0" Then
		CellVal = ""
	Else
		CellVal = a
	End If
End Function

'________________________________________________
' String right-justified
Function StrRightJstfd( s, Length, Fillchar )
	StrRightJstfd = String( Length - Len( s ), Fillchar ) & s
End Function

'________________________________________________
' Function Date2Num( d )
' устаревшее
' используйте функцию ToDGDate в новых программах 
'________________________________________________
Function Date2Num( d )
	Date2Num = Replace( FormatDate2( d, "dd/mm/yyyy" ), "/", "" )
End Function

'________________________________________________
' источник http://www.rondebruin.nl/windowsxpzip.htm
Function Zip_All_Files_in_Folder( folder, dest )
	Dim oApp, f

	'Create empty Zip File
	Set f = CreateObject( "Scripting.FileSystemObject" ).CreateTextFile( dest, True )
	f.Write Chr(80) & Chr(75) & Chr(5) & Chr(6) & String(18, 0)
	f.Close
	Set f = Nothing
	Sleep1

	Set oApp = CreateObject( "Shell.Application" )
	'Copy the files to the compressed folder
	oApp.Namespace( dest ).CopyHere oApp.Namespace( folder ).items

	'Keep script waiting until Compressing is done
	On Error Resume Next
	Do
		Sleep1
	Loop Until oApp.Namespace( dest ).items.Count = oApp.Namespace( folder ).items.Count

	If Err.Number = 0 Then
		Zip_All_Files_in_Folder = True
	Else
		Zip_All_Files_in_Folder = False
		If Len( Err.Description ) > 0 Then
			MsgBox "№ " & Err.Number & " - " & Err.Description & "; ", vbCritical
		End If
	End If
End Function

'________________________________________________
Sub Sleep1
	Dim t
	t = Timer
	Do
		Doevent
	Loop Until ( Timer - t ) > 0.5
End Sub

'________________________________________________
Function CreatePFZ( folder, path, fname )
	Dim s
	Dim Sys

	Set Sys = CreateLibObject("System")

	s = Left( fname, Len( fname ) - 3 ) & "zip"

	If Zip_All_Files_in_Folder( folder, path & s ) Then
		CreatePFZ = True
		If Isfileexists(path & fname) Then Sys.DeleteFile path & fname
		CreateObject( "Scripting.FileSystemObject" ).GetFile( path & s ).Name = fname
	Else
		CreatePFZ = False
	End If
End Function
